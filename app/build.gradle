apply plugin: 'com.android.application'

repositories {
    google()

    maven {
        url "https://jitpack.io"
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.farmerbb.secondscreen"
        minSdkVersion 17
        targetSdkVersion 30

        versionCode 183
        versionName "2.9.1"

        resConfigs "en", "es", "fr", "sk", "nl", "it", "zh"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "float", "TESTED_API_VERSION", "29.0f"
        buildConfigField "String", "SUPPORT_APPLICATION_ID", "\"com.farmerbb.secondscreen.support\""
        buildConfigField "long", "TIMESTAMP", "${System.currentTimeMillis()}L"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            if(System.getenv("KSTOREFILE") != null)
                storeFile file(System.getenv("KSTOREFILE"))

            storePassword System.getenv("KSTOREPWD")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/app_name_debug"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name"]

            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${project.parent.name}-${defaultConfig.versionName}.apk"
                }
            }
        }
    }

    flavorDimensions "default"

    productFlavors {
        paid {
            applicationId "com.farmerbb.secondscreen"
        }

        free {
            applicationId "com.farmerbb.secondscreen.free"
        }
    }

    sourceSets {
        main {
            java { srcDirs('src/main/java', 'src/support/java') }
            res { srcDirs('src/main/res') }
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'eu.chainfire:libsuperuser:1.1.0.202004101746'
    //noinspection GradleDependency
    implementation 'me.weishu:free_reflection:2.2.0'
    implementation 'moe.banana:toast-compat:1.0.5'
    implementation group:'com.twofortyfouram', name:'android-plugin-api-for-locale', version:'[1.0.2,2.0['
}
