apply plugin: 'com.android.application'

ext {
    SUPPORT_LIBRARY_VERSION = "27.0.2"
}

repositories {
    google()

    maven {
        url "https://jitpack.io"
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.farmerbb.secondscreen"
        minSdkVersion 17

        // Keeping target SDK at 25 for now
        targetSdkVersion 25

        versionCode 164
        versionName "2.6.5"

        resConfigs "en", "es", "fr", "sk"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            if(System.getenv("KSTOREFILE") != null)
                storeFile file(System.getenv("KSTOREFILE"))

            storePassword System.getenv("KSTOREPWD")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/app_name_debug"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name"]
        }
    }

    flavorDimensions "default"

    productFlavors {
        paid {
            applicationId "com.farmerbb.secondscreen"
        }

        free {
            applicationId "com.farmerbb.secondscreen.free"
        }
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation 'eu.chainfire:libsuperuser:1.0.0.201704021214'
    implementation 'moe.banana:toast-compat:1.0.5'
    implementation group:'com.twofortyfouram', name:'android-plugin-api-for-locale', version:'[1.0.2,2.0['
}