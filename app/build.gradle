apply plugin: 'com.android.application'

repositories {
    google()

    maven {
        url "https://jitpack.io"
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.farmerbb.secondscreen"
        minSdkVersion 17

        /* targetSdkVersion is explicitly set to API 27.
         * This is the earliest API level that Google still allows apps to be published on the
         * Play Store with, and also happens to be the latest API level that Android allows using
         * reflection to set resolution / DPI on non-rooted devices with (on Android 9 and later)
         *
         * After November 1, 2019, the following changes will need to be made to SecondScreen:
         *
         * - Add verbiage somewhere in the app (a snackbar, maybe?) encouraging users to download
         * the SecondScreen Support Library as that will be the only way for users on non-rooted
         * devices running Android 9 or later to set resolution / DPI without a reboot
         *
         * - Re-implement the changes made in commit 434328b (and reverted in 3d36992) setting
         * the default UI refresh method to "soft reboot" on Android 9 and later
         */
        //noinspection OldTargetApi
        targetSdkVersion 27

        versionCode 183
        versionName "2.9.1"

        resConfigs "en", "es", "fr", "sk", "nl", "it", "zh"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "float", "TESTED_API_VERSION", "29.0f"
        buildConfigField "String", "SUPPORT_APPLICATION_ID", "\"com.farmerbb.secondscreen.support\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            if(System.getenv("KSTOREFILE") != null)
                storeFile file(System.getenv("KSTOREFILE"))

            storePassword System.getenv("KSTOREPWD")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/app_name_debug"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name"]

            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${project.parent.name}-${defaultConfig.versionName}.apk"
                }
            }
        }
    }

    flavorDimensions "default"

    productFlavors {
        paid {
            applicationId "com.farmerbb.secondscreen"
        }

        free {
            applicationId "com.farmerbb.secondscreen.free"
        }
    }

    sourceSets {
        main {
            java { srcDirs('src/main/java', 'src/support/java') }
            res { srcDirs('src/main/res') }
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'eu.chainfire:libsuperuser:1.1.0.201907261845'
    implementation 'moe.banana:toast-compat:1.0.5'
    implementation group:'com.twofortyfouram', name:'android-plugin-api-for-locale', version:'[1.0.2,2.0['
}
