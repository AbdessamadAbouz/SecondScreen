apply plugin: 'com.android.application'

ext {
    SUPPORT_LIBRARY_VERSION = "28.0.0"
}

repositories {
    google()

    maven {
        url "https://jitpack.io"
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.farmerbb.secondscreen"
        minSdkVersion 17

        //noinspection ExpiringTargetSdkVersion
        targetSdkVersion 25

        versionCode 180
        versionName "2.8.2"

        resConfigs "en", "es", "fr", "sk", "nl"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "float", "TESTED_API_VERSION", "28.0f"
        buildConfigField "String", "SUPPORT_APPLICATION_ID", "\"com.farmerbb.secondscreen.support\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            if(System.getenv("KSTOREFILE") != null)
                storeFile file(System.getenv("KSTOREFILE"))

            storePassword System.getenv("KSTOREPWD")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/app_name_debug"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name"]

            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${project.parent.name}-${defaultConfig.versionName}.apk"
                }
            }
        }
    }

    flavorDimensions "default"

    productFlavors {
        paid {
            applicationId "com.farmerbb.secondscreen"
        }

        free {
            applicationId "com.farmerbb.secondscreen.free"
        }
    }

    sourceSets {
        main {
            java { srcDirs('src/main/java', 'src/support/java') }
            res { srcDirs('src/main/res') }
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
    }

}

dependencies {
    implementation "com.android.support:support-v4:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'eu.chainfire:libsuperuser:1.0.0.201811281328'
    implementation 'moe.banana:toast-compat:1.0.5'
    implementation group:'com.twofortyfouram', name:'android-plugin-api-for-locale', version:'[1.0.2,2.0['
}
