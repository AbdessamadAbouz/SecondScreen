apply plugin: 'com.android.application'

ext {
    SUPPORT_LIBRARY_VERSION = "25.3.1"
}

repositories {
    maven {
        url "https://maven.google.com"
    }

    maven {
        url "https://jitpack.io"
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.farmerbb.secondscreen"
        minSdkVersion 17
        targetSdkVersion 25

        versionCode 154
        versionName "2.6.2"

        resConfigs "en", "es", "fr", "sk"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            if(System.getenv("KSTOREFILE") != null)
                storeFile file(System.getenv("KSTOREFILE"))

            storePassword System.getenv("KSTOREPWD")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/app_name_debug"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name"]
        }
    }

    productFlavors {
        paid {
            applicationId "com.farmerbb.secondscreen"
        }

        free {
            applicationId "com.farmerbb.secondscreen.free"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'eu.chainfire:libsuperuser:1.0.0.201704021214'
    compile 'moe.banana:toast-compat:1.0.5'
    compile group:'com.twofortyfouram', name:'android-plugin-api-for-locale', version:'[1.0.2,2.0['
}